plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "me.champeau.jmh" version "0.7.1"
}

sourceCompatibility = 1.8
group = "com.wavesplatform"
version = "0.6.6-SNAPSHOT"
archivesBaseName = "curve25519-java"

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', project(':common').file('src/main/java')]
        }
    }
}

jmh {
    humanOutputFile = null
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.13.0'
    testImplementation project(':tests')
    jmh 'org.openjdk.jmh:jmh-core:1.4.1'
}

signing {
    useInMemoryPgpKeys(gpgKey, gpgPassphrase)
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'curve25519-java'

            from(components.java)

            pom {
                description = 'Curve25519 library for Java'
                url = 'https://github.com/WhisperSystems/curve25519-java'

                scm {
                    url = 'scm:git@github.com:WhisperSystems/curve25519-java.git'
                    connection = 'scm:git@github.com:WhisperSystems/curve25519-java.git'
                    developerConnection = 'scm:git@github.com:WhisperSystems/curve25519-java.git'
                }

                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Trevor Perrin'
                        name = 'Moxie Marlinspike'
                    }
                }
            }
        }
    }
}


task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
}

artifacts {
    archives(packageJavadoc) {
        type = 'javadoc'
    }

    archives packageSources
}
